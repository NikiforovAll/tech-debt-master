@echo off
setlocal enabledelayedexpansion
:: The scipt is designed to automate and simplify prompt development process.
:: It performs next list of tasks:
::  1. Copy target repository to new folder placed in the same directory.
::  2. Analyze copied content using tdm.
::  3. Add configuration, prompt and report to the target folder.

:: 1. Define folder with test project
set "SOURCE_FOLDER=C:\tdm-related\test-projects\MonkeyMCP-main"

:: 2. Get current date and time
for /f "tokens=1-4 delims=/ " %%a in ("%date%") do (
    set "mm=%%a"
    set "dd=%%b"
    set "yyyy=%%c"
)

for /f "tokens=1-3 delims=:." %%a in ("%time%") do (
    set "hh=%%a"
    set "min=%%b"
    set "sec=%%c"
)

:: Clean up time formatting (remove leading spaces/zeros if needed)
set "hh=0%hh%"
set "hh=%hh:~-2%"
set "min=0%min%"
set "min=%min:~-2%"
set "sec=0%sec%"
set "sec=%sec:~-2%"

:: Create a timestamp suffix
set "TIMESTAMP=%yyyy%-%mm%-%dd%_%hh%-%min%-%sec%"

:: 3. Create new folder name
set "NEW_FOLDER=%SOURCE_FOLDER%_%TIMESTAMP%"

:: 4. Copy folder and rename
xcopy "%SOURCE_FOLDER%" "%NEW_FOLDER%\src" /E /I /H /Y

:: 5 Copy prompt
:: Execution directory
xcopy "..\..\TechDebtMaster.Cli\Templates\techdebt-v2.prompty" "..\..\..\Artefacts\Templates\techdebt-v2.prompty" /Y
:: Report directory for further review
xcopy "..\..\TechDebtMaster.Cli\Templates\techdebt-v2.prompty" "%NEW_FOLDER%" /Y


:: 7. Run tdm commands
..\..\..\Artefacts\TechDebtMaster.Cli.exe config show >> "%NEW_FOLDER%\config.txt"
..\..\..\Artefacts\TechDebtMaster.Cli.exe repo index "%NEW_FOLDER%\src"
..\..\..\Artefacts\TechDebtMaster.Cli.exe debt analyze "%NEW_FOLDER%\src"
..\..\..\Artefacts\TechDebtMaster.Cli.exe debt report "%NEW_FOLDER%\src"

:: 9. Move renamed report
move "tech-debt-report.html" "%NEW_FOLDER%\tech-debt-report.html"

:: 10. Move artifacts generated by tdm
move "./.tdm" "%NEW_FOLDER%/.tdm"

:: 10. Open the report file
start "" "file:///%NEW_FOLDER%\tech-debt-report.html"

endlocal
